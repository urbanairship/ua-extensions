group = 'com.urbanairship.android'
version = '1.1.0'
description = "Urban Airship event templates"


apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 1
        versionName project.version
    }
}

dependencies {
    compile 'com.urbanairship.android:urbanairship-sdk:7.0.+'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.robolectric:shadows-support-v4:3.0'
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId project.group
            artifactId 'event-templates'

            name project.name
            description project.description
            url 'http://www.urbanairship.com'
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    configurations = ['archives']

    pkg {
        userOrg = 'urbanairship'
        name = project.name
        desc = project.description
        websiteUrl = 'http://www.urbanairship.com'

        version {
            repo = 'android'
            publish = false
            desc = project.description
            name = project.version
            released = new Date()
        }
    }
}
